ACTION_DEFINE_ASSOCIATIVE_ARRAY item_type BEGIN
//  5  => ~arrow~
//  14 => ~bullet~
  15 => ~bow~
  16 => ~dagger~
  17 => ~mace~
  18 => ~sling~
  19 => ~small sword~
  20 => ~big sword~
  21 => ~hammer~
  22 => ~morning star~
  23 => ~flail~
//  24 => ~dart~
  25 => ~axe~
  26 => ~staff~
  27 => ~crossbow~
  28 => ~hth~
  29 => ~spear~
  30 => ~polearm~
//  31 => ~bolt~
  57 => ~great sword~
  69 => ~bastard sword~
END 

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE  0x18 flags //need only bit 6
  READ_SHORT 0x1c type
  READ_BYTE  0x31 proficiency
  READ_LONG  0x60 enchantment
  magic_flag = (flags BAND 0b01000000) >> 6
  PATCH_IF (enchantment != 0 AND magic_flag = 0) AND VARIABLE_IS_SET $item_type(~%type%~) BEGIN
    PATCH_PRINT ~HI60: %SOURCE_FILE% - magic flag %magic_flag% and enchantment level %enchantment% are inconsistent. Setting magic flag.~
    new_flags = flags BOR 0b01000000
    WRITE_BYTE 0x18 new_flags
  END
  PATCH_IF (enchantment = 0 AND magic_flag != 0) AND VARIABLE_IS_SET $item_type(~%type%~) BEGIN
    PATCH_PRINT ~HI61: %SOURCE_FILE% - magic flag %magic_flag% and enchantment level %enchantment% are inconsistent.~
  END
BUT_ONLY
